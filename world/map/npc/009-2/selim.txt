009-2,32,104,0|script|Selim|326
{
    goto L_Main;

L_Main:
    mes "[Selim]";
    mes "\"Greetings, fair traveler. With what may I help you?\"";
    menu
        "Greetings, are you the store merchant?", L_ask_selim,
        "But what are dyes and what purpose do they serve?", L_ask_dye,
        "Can you dye something for me?", L_clothes_choice,
        "Can you also remove color from clothing?", L_ask_bleach,
        "About these sorcerer robes...", L_asksorcererrobe,
        "Never mind.", L_Close;

L_ask_bleach:
    mes "[Selim]";
    mes "\"I'm afraid no.";
    mes "For that, you would need to visit Candide in the Tulimshar marketplace.\"";
    goto L_Main;

L_ask_selim:
    mes "[Selim]";
    mes "\"That is an honest mistake you have made.";
    mes "However I am not the merchant, but rather a Master Dyer here to serve the good people of this town.";
    mes "I dye things, upon request.\"";
    menu
        "But what are dyes and where can I get them?", L_ask_dye,
        "Can you dye something for me?", L_clothes_choice,
        "Can you also remove color from clothing?", L_ask_bleach,
        "Good bye.", L_Close;

L_ask_dye:
    mes "[Selim]";
    mes "\"Dyes are special liquids used to add color to clothing. If you want me to dye something for you you will have to bring me some dye first.";
    mes "Those behind me are nearly empty and won't last for your item.\"";
    menu
        "What do you mean, `bring you dye'?", L_explain_dye,
        "Where would I get dye?", L_explain_dye,
        "Good bye.", L_Close;

L_clothes_choice:
    npcaction 9; // clear dialog window
    mes "[Selim]";
    mes "\"What can I dye for you today?\"";
    next;
    mes "%%B ##3Drag and drop an item from your inventory to the box below.##0";
    requestitem .@item$[0];
    if (.@item$[0] == "") goto L_Sorry;
    set .@c, 0;
    set .@m, getarraysize(.colors$);
    goto L_CheckColor;

L_CheckColor:
    // make sure this item can be dyed in all colors
    if (getitemlink(.colors$[.@c] + .@item$[0]) == "Unknown Item") goto L_Sorry;
    set .@c, .@c + 1;
    if (.@c < .@m) goto L_CheckColor;
    setarray @selim$[0], .@item$[0];
    goto L_colour;

L_Sorry:
    setarray @selim$[0], ""; // just in case
    mes "[Selim]";
    mes "\"Sorry, I can not dye this item.";
    mes "Would you like to try again?\"";
    next;
    menu
        "Yes", L_clothes_choice,
        "Never mind.", L_Close;

L_asksorcererrobe:
    mes "[Selim]";
    mes "\"Yes?\"";
    next;
    menu
        "Do you think you can dye that line to a different color?", L_Next1;

L_Next1:
    mes "[Selim]";
    mes "\"Hum, I fear I can't do that. The area is too small and I can operate only on large ones.";
    mes "But with the appropriate materials, maybe a talented tailor can make the change.\"";
    next;
    menu
        "Oh, I see!", L_Next;

L_Next:
    mes "[Selim]";
    mes "\"Of course I can help you by dyeing the materials your tailor will want.";
    mes "All I need is a piece of Cotton Cloth.\"";
    if (countitem("CottonCloth") > 0) setarray @selim$[0], "CottonCloth";
    menu
        "Sure. Here is one.", L_colour,
        "I have to go pick that.", L_Close;

L_colour:
    npcaction 9; // clear dialog window
    mes "[Selim]";
    mes "\"Excellent. Now, what color do you want?\"";
    next;
    mes "%%B ##3Drag and drop a dye from your inventory to the box below.##0";
    requestitem .@dye$;
    if (.@dye$ == "") goto L_no_dye;
    set .@index, array_search(.@dye$[0], .dyes$);
    if (.@index < 0) goto L_no_dye;
    setarray @selim$[1], .@dye$[0], .colors$[.@index];
    goto L_finish;

L_no_dye:
    mes "[Selim]";
    mes "\"I would love to dye your " + getitemlink(@selim$[0]) + " for you, but you will have to bring me some dye first.";
    mes "Those behind me are nearly empty and won't last for your item.\"";
    next;
    menu
        "I wanted to dye a different item anyway.", L_clothes_choice,
        "What do you mean, `bring you dye'?", L_explain_dye,
        "Where would I get dye?", L_explain_dye,
        "Never mind.", L_Close;

L_explain_dye:
    if (QUEST_clothdyer_knowsdye < 1)
        set QUEST_clothdyer_knowsdye, 1;
    mes "[Selim]";
    mes "\"Well, dye is very expensive, and since I don't charge adventurers anything, I can't give you any for free.";
    mes "But most alchemists can make dye; perhaps you can find one around here.\"";
    goto L_Close;

L_finish:
    debugmes "`"+@selim$[0]+"`, `"+@selim$[1]+"`, `"+@selim$[2]+"`";
    set .@i$, @selim$[2] + @selim$[0];
    if (getitemlink(.@i$) == "Unknown Item") goto L_Sorry;
    if (countitem(@selim$[1]) > 0) delitem @selim$[1], 1;
    else goto L_Cheat;
    if (countitem(@selim$[0]) > 0) delitem @selim$[0], 1;
    else goto L_Cheat;
    getitem .@i$, 1;
    mes "[Selim]";
    mes "\"Here's your " + getitemlink(.@i$) + "!  Please let dry for a minute.\"";
    goto L_Close;

L_Cheat:
    mes "[Selim]";
    mes "\"Don't try to cheat me!\"";
    next;
    goto L_Close;

L_Close:
    mes "\"Farewell and good luck in your journeys!\"";
    close2;
    emotion EMOTE_WINK, strcharinfo(0);
    end;

OnInit:
    setarray .colors$, "Red", "Green", "DarkBlue", "Yellow", "LightBlue", "Pink", "Black", "Orange", "Purple", "DarkGreen";
    setarray .dyes$, "RedDye", "GreenDye", "DarkBlueDye", "YellowDye", "LightBlueDye", "PinkDye", "BlackDye", "OrangeDye", "PurpleDye", "DarkGreenDye";
    end;
}
