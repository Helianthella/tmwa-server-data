-|script|@divorce|32767
{
    callfunc "argv_splitter";
    set .@n$, if_then_else(@argv$[0] != "", "char", "") + "divorce";
    if (GM < CMD_DIVORCE && GM < G_SYSOP) goto L_GM; // check if you can use it on self
    set .@target_id, BL_ID;
    if (@argv$[0] != "") set .@target_id, getcharid(3, @argv$[0]);
    if (@argv$[0] != "" && !(isloggedin(.@target_id))) set .@target_id, getcharid(3, @args$);
    if (@argv$[0] != "" && !(isloggedin(.@target_id))) goto L_Failed; // do NOT fallback to self
    if (@argv$[0] != "" && GM < CMD_CHARDIVORCE && GM < G_SYSOP) goto L_GM; // when target is not self, use chardivorce permission

    set PARTNER, 0, .@target_id;
    gmlog "@"+.@n$+" " + @args$;
    message strcharinfo(0), .@n$+" : The operation succeeded.";
    end;

L_Failed:
    message strcharinfo(0), .@n$+" : Impossible to attach to the target player. Did you try putting the name in \"quotation marks\"?";
    end;

L_GM:
    message strcharinfo(0), .@n$+" : GM command is level "+ if_then_else(@argv$[0] != "", CMD_CHARDIVORCE, CMD_DIVORCE) +", but you are level " + GM;
    end;

OnInit:
    registercmd chr(ATCMD_SYMBOL) + "divorce", strnpcinfo(0);
    registercmd chr(ATCMD_SYMBOL) + "chardivorce", strnpcinfo(0);
    end;
}
